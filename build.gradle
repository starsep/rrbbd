plugins {
    id "me.tatarka.retrolambda" version "3.6.0"
}

group 'com.starsep'
version '0.3.2'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.starsep.rrbbd.core.Main'

configurations {
    checkstyleOverride
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'com.j2html', name: 'j2html', version: '0.88'
    compile group: 'com.healthmarketscience.jackcess', name: 'jackcess', version: '2.1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    checkstyleOverride('com.puppycrawl.tools:checkstyle:7.6.1')
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": mainClassName
        )
    }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": mainClassName
        )
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task createBuildInfoFile(dependsOn: processResources) << {
    def buildInfoFile = new File("$buildDir/resources/main/build.properties")
    Properties props = new Properties()
    props.setProperty('version', project.version.toString())
    props.store(buildInfoFile.newWriter(), null)
}
classes {
    dependsOn createBuildInfoFile
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
}

tasks.withType(Checkstyle) {
    checkstyleClasspath = project.configurations.checkstyleOverride
}